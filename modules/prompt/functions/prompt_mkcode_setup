#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_mkcode_precmd {

  # Git info
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python information.
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
      ruby-info
  fi

  # Get node information.
  if (( $+functions[node-info] )); then
      node-info
  fi
}

function prompt_mkcode_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_mkcode_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mkcode_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_mkcode_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b '
  zstyle ':prezto:module:git:info:added' format '%%B%F{2}✚%f%%b '
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{13}⬆%f%%b '
  zstyle ':prezto:module:git:info:behind' format '%%B%F{13}⬇%f%%b '
  zstyle ':prezto:module:git:info:branch' format '%%B${_prompt_mkcode_colors[1]}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format '%%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{1}✖%f%%b '
  zstyle ':prezto:module:git:info:modified' format '%%B%F{4}✱%f%%b '
  zstyle ':prezto:module:git:info:position' format '%%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{5}➜%f%%b '
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{6}✭%f%%b '
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{3}═%f%%b '
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{7}◼%f%%b '
  zstyle ':prezto:module:git:info:keys' format \
         'status' 'at $(coalesce "%b" "%p" "%c") %s%A%B%S%a%d%m%r%U%u'
         # 'rprompt' '%F{blue}git[%f%c%F{blue}]%f'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '${_prompt_mkcode_colors[5]}...%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{blue}py[%f%%B%F{13}%v%f%%b%F{blue}]%f'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{blue}rb[%f%%B%F{13}%v%f%%b%F{blue}]%f'

  # Set node-info parameters.
  zstyle ':prezto:module:node:info:version' format '%F{blue}js[%f%%B%F{13}%v%f%%b%F{blue}]%f'

  # Define prompts.
  VIM_PROMPT="using ${_prompt_mkcode_colors[4]}[% NORMAL]% %f "
  PROMPT="
${_prompt_mkcode_colors[3]}%n%f at ${_prompt_mkcode_colors[2]}%m%f "'${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/}'"in ${_prompt_mkcode_colors[5]}%~%f "'${git_info:+${(e)git_info[status]}}'"
"''"$ "
  RPROMPT='${python_info[version]} ${ruby_info[version]} $node_info[version]' # ${git_info[rprompt]}'
  SPROMPT='zsh: correct ${_prompt_mkcode_colors[3]}%R%f to ${_prompt_mkcode_colors[5]}%r%f [nyae]? '
}

function zle-line-init zle-keymap-select {
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

export KEYTIMEOUT=15

prompt_mkcode_setup "$@"
